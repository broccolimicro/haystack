record dualrail
{
	node<1> t;
	node<1> f;
}

operator&(node<1> s, node<1> a, node<1> b)
{
	[	 a & b -> s+
	[]	~a | ~b -> s-
	]
}

channel achan4p0b
{
	node<1> r;
	node<1> a;
	
	operator!()
	{
		r+; [a]; r-; [~a];
	}
	
	operator?()
	{
		[r]; a+; [~r]; a-;
	}
	
	operator@()
	{
		r
	}
}

channel achan4p1b
{
	dualrail r;
	node<1> a;
	
	operator!(node<1> i)
	{
		[	i  -> r.t,r.f := 1,0
		[]	~i -> r.t,r.f := 0,1
		];[a]; r.t,r.f := 0,0; [~a];
	}
	
	operator?(node<1> i)
	{
		[r.t | r.f]; i := r.t & ~r.f; a+; [~r.t & ~r.f]; a-;
	}
	
	operator@()
	{
		a
	}
}

inline process mux1b2(achan4p1b A, achan4p1b B, achan4p1b S, achan4p1b O)
{
	node<1> s;
	node<1> o;
	*[
		S?s;
		[ ~s -> A?o;O!o
		[] s -> B?o;O!o
		];
	]
}

inline process fifo(achan4p0b L, achan4p0b R)
{
	*[L?;R!]
}

process chip(achan4p0b a, achan4p0b b)
{
	*[a?;b!]
}